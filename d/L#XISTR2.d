BEGIN L#XISTR2

//INITIAL

CHAIN IF ~Global("L#XIGreenManQuest","GLOBAL",0)~ THEN L#XISTR2 00.00
@0
END
IF~~THEN REPLY @1 EXTERN L#XISTR2 00.01
IF~~THEN REPLY @2 EXTERN L#XISTR2 00.02

CHAIN L#XISTR2 00.01
@3
== L#XIKJ IF ~InParty("L#XIK") !StateCheck("L#XIK",CD_STATE_NOTVALID)~ THEN @4
==L#XISTR2 @5
EXTERN L#XISTR2 00.03

CHAIN L#XISTR2 00.02
@6
EXTERN L#XISTR2 00.03

CHAIN L#XISTR2 00.03
@7
END
IF~~THEN REPLY @8 EXTERN L#XISTR2 00.04
IF~~THEN REPLY @9 EXTERN L#XISTR2 00.04
IF~~THEN REPLY @10 EXTERN L#XISTR2 00.04

CHAIN L#XISTR2 00.04
@11
==L#XISTR2 @12
==L#XISTR2 @13
END
IF~PartyHasItem("MISC30")~THEN REPLY @14 EXTERN L#XISTR2 00.05
IF~~THEN REPLY @15 EXTERN L#XISTR2 00.06
IF~~THEN REPLY @16 EXTERN L#XISTR2 00.07

CHAIN L#XISTR2 00.05
@17
EXTERN L#XISTR2 00.08

CHAIN L#XISTR2 00.06
@18
EXTERN L#XISTR2 00.08

CHAIN L#XISTR2 00.07
@19
EXTERN L#XISTR2 00.08

CHAIN L#XISTR2 00.08
@20
END
IF~~THEN REPLY @21 EXTERN L#XISTR2 00.09
IF~~THEN REPLY @22 EXTERN L#XISTR2 00.10
IF~~THEN REPLY @23 EXTERN L#XISTR2 00.11

CHAIN L#XISTR2 00.09
@24
EXTERN L#XISTR2 00.12

CHAIN L#XISTR2 00.10
@25
EXTERN L#XISTR2 00.12

CHAIN L#XISTR2 00.12
@26
==L#XISTR2 @27 
DO ~SetGlobal("L#XIGreenManQuest","GLOBAL",1)~ EXIT

CHAIN L#XISTR2 00.11
@28
DO ~SetGlobal("L#XIGreenManQuest","GLOBAL",1)~ EXIT

//2ND

CHAIN IF ~Global("L#XIGreenManQuest","GLOBAL",1)~ THEN L#XISTR2 01.00
@29
END
IF~NumItemsPartyGT("MISC30",6)~THEN REPLY @30 DO ~TakePartyItemNum("MISC30",7)~ EXTERN L#XISTR2 01.01
IF~~THEN REPLY @31 EXTERN L#XISTR2 01.02
IF~~THEN REPLY @32 EXTERN L#XISTR2 01.03

CHAIN L#XISTR2 01.02
@33
EXIT

CHAIN L#XISTR2 01.03
@34
EXIT

CHAIN L#XISTR2 01.01
@35
DO ~StartCutSceneMode() SetGlobal("L#XIGreenManQuest","GLOBAL",2) Wait(1) TriggerActivation("L#XISTRI",FALSE) ScreenShake([20.20],20) AmbientActivate("L#XISTRI",FALSE) Wait(2) CreateCreature("L#XIGRE",[835.450],0) SmallWait(1) CreateVisualEffectObject("SPPLANAR","L#XIGRE") Wait(2) EndCutSceneMode() DestroySelf()~ EXIT

////////////////////////
////////////////////////

BEGIN L#XIGRE

CHAIN IF ~Global("L#XIGreenManQuest","GLOBAL",2)~ THEN L#XIGRE 02.00
@36
==L#XIGRE @37
END
IF~~THEN REPLY @38 EXTERN L#XIGRE 02.01
IF~~THEN REPLY @39 EXTERN L#XIGRE 02.02

CHAIN L#XIGRE 02.01
@40
EXTERN L#XIGRE 02.03

CHAIN L#XIGRE 02.02
@41
EXTERN L#XIGRE 02.03

CHAIN L#XIGRE 02.03
@42
==L#XIGRE @43
END
IF~~THEN REPLY @44 EXTERN L#XIGRE 02.04
IF~~THEN REPLY @45 EXTERN L#XIGRE 02.04

CHAIN L#XIGRE 02.04
@46
DO ~SetGlobal("L#XIGreenManQuest","GLOBAL",3) Enemy()~ EXIT

